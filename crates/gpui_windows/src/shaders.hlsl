#include "alpha_correction.hlsl"

cbuffer GlobalParams: register(b0) {
    float4 gamma_ratios;
    float2 global_viewport_size;
    float grayscale_enhanced_contrast;
    float subpixel_enhanced_contrast;
    uint is_bgr;
    uint3 global_pad;
};

cbuffer BatchParams: register(b1) {
    uint batch_start_index;
    uint3 batch_pad;
};

Texture2D<float4> t_sprite: register(t0);
Texture2D<float4> t_backdrop_sharp: register(t1);
SamplerState s_sprite: register(s0);

struct SubpixelSpriteFragmentOutput {
    float4 foreground : SV_Target0;
    float4 alpha : SV_Target1;
};

struct Bounds {
    float2 origin;
    float2 size;
};

struct Corners {
    float top_left;
    float top_right;
    float bottom_right;
    float bottom_left;
};

struct Edges {
    float top;
    float right;
    float bottom;
    float left;
};

struct ClipNode {
    Bounds bounds;
    Corners radii;
    uint2 parent;
};
StructuredBuffer<ClipNode> rounded_clips: register(t2);
float quad_sdf(float2 pt, Bounds bounds, Corners corner_radii);

float rounded_clip_coverage(float2 position, uint id) {
    float coverage = 1.;
    [loop] while (id != 0u) {
        ClipNode node = rounded_clips[id - 1u];
        if (any(node.bounds.size <= 0.)) return 0.;
        coverage = min(coverage, saturate(0.5 - quad_sdf(position, node.bounds, node.radii)));
        id = node.parent.x;
    }
    return coverage;
}

float4 clipped_color(float4 color, float2 position, uint id, bool premultiplied) {
    if (id == 0u) return color;
    float coverage = rounded_clip_coverage(position, id);
    return color * float4(premultiplied ? coverage.xxx : float3(1., 1., 1.), coverage);
}

struct Hsla {
    float h;
    float s;
    float l;
    float a;
};

struct LinearColorStop {
    Hsla color;
    float percentage;
};

struct Background {
    // 0u is Solid
    // 1u is LinearGradient
    // 2u is PatternSlash
    // 3u is Checkerboard
    // 4u is RadialGradient
    // 5u is ConicGradient
    uint tag;
    // 0u is sRGB linear color
    // 1u is Oklab color
    uint color_space;
    Hsla solid;
    float gradient_angle_or_pattern_height;
    float2 gradient_center;
    float2 gradient_radius;
    LinearColorStop colors[8];
    uint color_stop_count;
};

struct GradientColor {
  float4 solid;
  float4 color0;
  float4 color1;
};

struct AtlasTextureId {
    uint index;
    uint kind;
};

struct AtlasBounds {
    int2 origin;
    int2 size;
};

struct AtlasTile {
    AtlasTextureId texture_id;
    uint tile_id;
    uint padding;
    AtlasBounds bounds;
};

struct TransformationMatrix {
    float2x2 rotation_scale;
    float2 translation;
};

static const float M_PI_F = 3.141592653f;
static const float3 GRAYSCALE_FACTORS = float3(0.2126f, 0.7152f, 0.0722f);

float4 to_device_position_impl(float2 position) {
    float2 device_position = position / global_viewport_size * float2(2.0, -2.0) + float2(-1.0, 1.0);
    return float4(device_position, 0., 1.);
}

float4 to_device_position(float2 unit_vertex, Bounds bounds) {
    float2 position = unit_vertex * bounds.size + bounds.origin;
    return to_device_position_impl(position);
}

float4 distance_from_clip_rect_impl(float2 position, Bounds clip_bounds) {
    float2 tl = position - clip_bounds.origin;
    float2 br = clip_bounds.origin + clip_bounds.size - position;
    return float4(tl.x, br.x, tl.y, br.y);
}

float4 distance_from_clip_rect(float2 unit_vertex, Bounds bounds, Bounds clip_bounds) {
    float2 position = unit_vertex * bounds.size + bounds.origin;
    return distance_from_clip_rect_impl(position, clip_bounds);
}

// The screen-space direction a local one carries, with the translation left
// off. The same multiply `to_device_position_transformed` applies, named
// separately so a fragment shader can ask what one local unit measures on
// screen without restating a matrix storage convention.
float2 transform_direction(float2 direction, TransformationMatrix transformation) {
    return mul(direction, transformation.rotation_scale);
}

float4 distance_from_clip_rect_transformed(float2 unit_vertex, Bounds bounds, Bounds clip_bounds, TransformationMatrix transformation) {
    float2 position = unit_vertex * bounds.size + bounds.origin;
    float2 transformed = transform_direction(position, transformation) + transformation.translation;
    return distance_from_clip_rect_impl(transformed, clip_bounds);
}

// Convert linear RGB to sRGB
float3 linear_to_srgb(float3 color) {
    return pow(color, float3(2.2, 2.2, 2.2));
}

// Convert sRGB to linear RGB
float3 srgb_to_linear(float3 color) {
    return pow(color, float3(1.0 / 2.2, 1.0 / 2.2, 1.0 / 2.2));
}

/// Hsla to linear RGBA conversion.
float4 hsla_to_rgba(Hsla hsla) {
    float h = hsla.h * 6.0; // Now, it's an angle but scaled in [0, 6) range
    float s = hsla.s;
    float l = hsla.l;
    float a = hsla.a;

    float c = (1.0 - abs(2.0 * l - 1.0)) * s;
    float x = c * (1.0 - abs(fmod(h, 2.0) - 1.0));
    float m = l - c / 2.0;

    float r = 0.0;
    float g = 0.0;
    float b = 0.0;

    if (h >= 0.0 && h < 1.0) {
        r = c;
        g = x;
        b = 0.0;
    } else if (h >= 1.0 && h < 2.0) {
        r = x;
        g = c;
        b = 0.0;
    } else if (h >= 2.0 && h < 3.0) {
        r = 0.0;
        g = c;
        b = x;
    } else if (h >= 3.0 && h < 4.0) {
        r = 0.0;
        g = x;
        b = c;
    } else if (h >= 4.0 && h < 5.0) {
        r = x;
        g = 0.0;
        b = c;
    } else {
        r = c;
        g = 0.0;
        b = x;
    }

    float4 rgba;
    rgba.x = (r + m);
    rgba.y = (g + m);
    rgba.z = (b + m);
    rgba.w = a;
    return rgba;
}

// Converts a sRGB color to the Oklab color space.
// Reference: https://bottosson.github.io/posts/oklab/#converting-from-linear-srgb-to-oklab
float4 srgb_to_oklab(float4 color) {
    // Convert non-linear sRGB to linear sRGB
    color = float4(srgb_to_linear(color.rgb), color.a);

    float l = 0.4122214708 * color.r + 0.5363325363 * color.g + 0.0514459929 * color.b;
    float m = 0.2119034982 * color.r + 0.6806995451 * color.g + 0.1073969566 * color.b;
    float s = 0.0883024619 * color.r + 0.2817188376 * color.g + 0.6299787005 * color.b;

    float l_ = pow(l, 1.0/3.0);
    float m_ = pow(m, 1.0/3.0);
    float s_ = pow(s, 1.0/3.0);

    return float4(
        0.2104542553 * l_ + 0.7936177850 * m_ - 0.0040720468 * s_,
        1.9779984951 * l_ - 2.4285922050 * m_ + 0.4505937099 * s_,
        0.0259040371 * l_ + 0.7827717662 * m_ - 0.8086757660 * s_,
        color.a
    );
}

// Converts an Oklab color to the sRGB color space.
float4 oklab_to_srgb(float4 color) {
    float l_ = color.r + 0.3963377774 * color.g + 0.2158037573 * color.b;
    float m_ = color.r - 0.1055613458 * color.g - 0.0638541728 * color.b;
    float s_ = color.r - 0.0894841775 * color.g - 1.2914855480 * color.b;

    float l = l_ * l_ * l_;
    float m = m_ * m_ * m_;
    float s = s_ * s_ * s_;

    float3 linear_rgb = float3(
        4.0767416621 * l - 3.3077115913 * m + 0.2309699292 * s,
        -1.2684380046 * l + 2.6097574011 * m - 0.3413193965 * s,
        -0.0041960863 * l - 0.7034186147 * m + 1.7076147010 * s
    );

    // Convert linear sRGB to non-linear sRGB
    return float4(linear_to_srgb(linear_rgb), color.a);
}

// This approximates the error function, needed for the gaussian integral
float2 erf(float2 x) {
    float2 s = sign(x);
    float2 a = abs(x);
    x = 1. + (0.278393 + (0.230389 + 0.078108 * (a * a)) * a) * a;
    x *= x;
    return s - s / (x * x);
}

float blur_along_x(float x, float y, float sigma, float corner, float2 half_size) {
    float delta = min(half_size.y - corner - abs(y), 0.);
    float curved = half_size.x - corner + sqrt(max(0., corner * corner - delta * delta));
    float2 integral = 0.5 + 0.5 * erf((x + float2(-curved, curved)) * (sqrt(0.5) / sigma));
    return integral.y - integral.x;
}

// A standard gaussian function, used for weighting samples
float gaussian(float x, float sigma) {
    return exp(-(x * x) / (2. * sigma * sigma)) / (sqrt(2. * M_PI_F) * sigma);
}

float4 over(float4 below, float4 above) {
    float4 result;
    float alpha = above.a + below.a * (1.0 - above.a);
    result.rgb = (above.rgb * above.a + below.rgb * below.a * (1.0 - above.a)) / alpha;
    result.a = alpha;
    return result;
}

float2 to_tile_position(float2 unit_vertex, AtlasTile tile) {
    float2 atlas_size;
    t_sprite.GetDimensions(atlas_size.x, atlas_size.y);
    return (float2(tile.bounds.origin) + unit_vertex * float2(tile.bounds.size)) / atlas_size;
}

float2 to_inset_tile_position(float2 unit_vertex, AtlasTile tile) {
    float2 atlas_size;
    t_sprite.GetDimensions(atlas_size.x, atlas_size.y);
    float2 first_center = float2(tile.bounds.origin) + 0.5;
    float2 center_span = max(float2(tile.bounds.size) - 1.0, 0.0);
    return (first_center + unit_vertex * center_span) / atlas_size;
}

// Selects corner radius based on quadrant.
float pick_corner_radius(float2 center_to_point, Corners corner_radii) {
    if (center_to_point.x < 0.) {
        if (center_to_point.y < 0.) {
            return corner_radii.top_left;
        } else {
            return corner_radii.bottom_left;
        }
    } else {
        if (center_to_point.y < 0.) {
            return corner_radii.top_right;
        } else {
            return corner_radii.bottom_right;
        }
    }
}

float4 to_device_position_transformed(float2 unit_vertex, Bounds bounds,
                                      TransformationMatrix transformation) {
    float2 position = unit_vertex * bounds.size + bounds.origin;
    float2 transformed = transform_direction(position, transformation) + transformation.translation;
    float2 device_position = transformed / global_viewport_size * float2(2.0, -2.0) + float2(-1.0, 1.0);
    return float4(device_position, 0.0, 1.0);
}

// Implementation of quad signed distance field
float quad_sdf_impl(float2 corner_center_to_point, float corner_radius) {
    if (corner_radius == 0.0) {
        // Fast path for unrounded corners
        return max(corner_center_to_point.x, corner_center_to_point.y);
    } else {
        // Signed distance of the point from a quad that is inset by corner_radius
        // It is negative inside this quad, and positive outside
        float signed_distance_to_inset_quad =
            // 0 inside the inset quad, and positive outside
            length(max(float2(0.0, 0.0), corner_center_to_point)) +
            // 0 outside the inset quad, and negative inside
            min(0.0, max(corner_center_to_point.x, corner_center_to_point.y));

        return signed_distance_to_inset_quad - corner_radius;
    }
}

float quad_sdf(float2 pt, Bounds bounds, Corners corner_radii) {
    float2 half_size = bounds.size / 2.;
    float2 center = bounds.origin + half_size;
    float2 center_to_point = pt - center;
    float corner_radius = pick_corner_radius(center_to_point, corner_radii);
    float2 corner_to_point = abs(center_to_point) - half_size;
    float2 corner_center_to_point = corner_to_point + corner_radius;
    return quad_sdf_impl(corner_center_to_point, corner_radius);
}

// The gradient of `quad_sdf` at the same point, in the same space.
//
// This exists so a primitive can size its antialiasing ramp without asking the
// rasterizer for a screen-space derivative of the distance. `fwidth` reads the
// neighbouring lanes of a 2x2 fragment quad, and a lane that belongs to the
// other triangle of a two-triangle rectangle is a helper invocation whose
// value some backends do not reconstruct from the same plane: llvmpipe returns
// derivatives in the hundreds of pixels along the shared edge, which collapses
// `distance / edge_width` to zero and leaves fully interior pixels at half
// coverage. The gradient is a property of the field, so deriving it here is
// both exact and independent of how any backend fills a helper lane.
//
// The field is a distance, so this vector has unit length wherever it is
// defined. It follows the same branches `quad_sdf_impl` takes.
float2 quad_sdf_gradient(float2 pt, Bounds bounds, Corners corner_radii) {
    float2 half_size = bounds.size / 2.;
    float2 center = bounds.origin + half_size;
    float2 center_to_point = pt - center;
    float corner_radius = pick_corner_radius(center_to_point, corner_radii);
    float2 corner_to_point = abs(center_to_point) - half_size;
    float2 corner_center_to_point = corner_to_point + corner_radius;

    // In the quadrant `abs` folded the point into.
    float2 mirrored = corner_center_to_point.x > corner_center_to_point.y
                          ? float2(1.0, 0.0)
                          : float2(0.0, 1.0);
    if (corner_radius != 0.0) {
        float2 outside = max(float2(0.0, 0.0), corner_center_to_point);
        float outside_distance = length(outside);
        if (outside_distance > 0.0) {
            mirrored = outside / outside_distance;
        }
    }

    // Unfold it. `sign` is zero on the centre lines, where either direction is
    // as good as the other, so the fold is undone with an explicit choice.
    float2 unfold = float2(center_to_point.x >= 0.0 ? 1.0 : -1.0,
                           center_to_point.y >= 0.0 ? 1.0 : -1.0);
    return mirrored * unfold;
}

// How wide, in device pixels, the antialiasing ramp of a transformed quad SDF
// is at one point.
//
// This is what a conforming `fwidth(distance)` reports — the sum of the two
// absolute screen-space partial derivatives — computed from the field and the
// primitive's own transform instead of from neighbouring fragments. A
// primitive whose transform collapses to a line has no ramp to measure, and
// falls back to one pixel.
float quad_sdf_edge_width(float2 pt, Bounds bounds, Corners corner_radii,
                          TransformationMatrix transformation) {
    float2 gradient = quad_sdf_gradient(pt, bounds, corner_radii);
    // The two screen-space vectors one local unit along each local axis maps
    // to, which are the columns of the forward transform. Naming them through
    // the same multiply the vertex stage does keeps every renderer's matrix
    // storage convention out of this.
    float2 along_x = transform_direction(float2(1.0, 0.0), transformation);
    float2 along_y = transform_direction(float2(0.0, 1.0), transformation);
    float mapped_area = along_x.x * along_y.y - along_y.x * along_x.y;
    if (abs(mapped_area) < 1e-6) {
        return 1.0;
    }
    // A gradient is a covector, so it travels through the inverse transpose.
    float2 screen_gradient =
        float2(along_y.y * gradient.x - along_x.y * gradient.y,
               along_x.x * gradient.y - along_y.x * gradient.x) /
        mapped_area;
    return max(abs(screen_gradient.x) + abs(screen_gradient.y), 0.0001);
}

GradientColor prepare_gradient_pair(uint color_space, Hsla color0, Hsla color1) {
    GradientColor output;
    output.color0 = hsla_to_rgba(color0);
    output.color1 = hsla_to_rgba(color1);

    if (color_space == 1) {
        output.color0 = srgb_to_oklab(output.color0);
        output.color1 = srgb_to_oklab(output.color1);
    }
    return output;
}

GradientColor prepare_gradient_color(uint tag, uint color_space, Hsla solid, LinearColorStop colors[8]) {
    GradientColor output;
    if (tag == 0 || tag == 2 || tag == 3) {
        output.solid = hsla_to_rgba(solid);
    } else if (tag == 1 || tag == 4 || tag == 5) {
        output = prepare_gradient_pair(color_space, colors[0].color, colors[1].color);
    }
    return output;
}

float2x2 rotate2d(float angle) {
    float s = sin(angle);
    float c = cos(angle);
    return float2x2(c, -s, s, c);
}

// lowbias32 from Hash Function Prospector, revision
// 396dbe235c94dfc2e9b559fc965bcfda8b6a122c (Unlicense).
uint lowbias32(uint x) {
    x ^= x >> 16u;
    x *= 0x7feb352du;
    x ^= x >> 15u;
    x *= 0x846ca68bu;
    x ^= x >> 16u;
    return x;
}

float gradient_dither(uint2 pixel) {
    uint seed =
        (pixel.x * 0x9e3779b9u) ^
        (pixel.y * 0x85ebca6bu);
    uint h0 = lowbias32(seed ^ 0xa511e9b3u);
    uint h1 = lowbias32(seed ^ 0x63d83595u);

    // The shifted values and power-of-two scale are exact in binary32.
    return ((float)(h0 >> 8u) - (float)(h1 >> 8u)) *
           (1.0f / 16777216.0f);
}

float4 gradient_color(Background background,
                      float2 position,
                      Bounds bounds,
                      float4 solid_color, float4 color0, float4 color1) {
    float4 color;

    switch (background.tag) {
        case 0:
            color = solid_color;
            break;
        case 1:
        case 4:
        case 5: {
            float t = 0.0;
            if (background.tag == 1) {
                // -90 degrees to match the CSS gradient angle.
                float gradient_angle = background.gradient_angle_or_pattern_height;
                float radians = (fmod(gradient_angle, 360.0) - 90.0) * (M_PI_F / 180.0);
                float2 direction = float2(cos(radians), sin(radians));

                // Expand the short side to be the same as the long side.
                if (bounds.size.x > bounds.size.y) {
                    direction.y *= bounds.size.y / bounds.size.x;
                } else {
                    direction.x *=  bounds.size.x / bounds.size.y;
                }

                float2 half_size = bounds.size * 0.5;
                float2 center = bounds.origin + half_size;
                float2 center_to_point = position - center;
                t = dot(center_to_point, direction) / length(direction);
                if (abs(direction.x) > abs(direction.y)) {
                    t = (t + half_size.x) / bounds.size.x;
                } else {
                    t = (t + half_size.y) / bounds.size.y;
                }
            } else if (background.tag == 4) {
                float2 center = bounds.origin + bounds.size * background.gradient_center;
                float2 radius = max(
                    abs(bounds.size * background.gradient_radius),
                    float2(0.0001, 0.0001)
                );
                t = length((position - center) / radius);
            } else {
                float2 center = bounds.origin + bounds.size * background.gradient_center;
                float2 center_to_point = position - center;
                if (dot(center_to_point, center_to_point) > 0.00000001) {
                    float clockwise_from_top = atan2(center_to_point.x, -center_to_point.y);
                    float start = background.gradient_angle_or_pattern_height * M_PI_F / 180.0;
                    t = frac((clockwise_from_top - start) / (2.0 * M_PI_F));
                }
            }

            // Select the two stops surrounding this pixel. The stop count and
            // fixed capacity match GPUI's renderer ABI on every backend.
            uint stop_count = clamp(background.color_stop_count, 2u, 8u);
            uint upper = stop_count - 1u;
            [unroll]
            for (uint index = 1u; index < 8u; index++) {
                if (index < stop_count && t <= background.colors[index].percentage) {
                    upper = index;
                    break;
                }
            }
            uint lower = upper - 1u;
            float start = background.colors[lower].percentage;
            float end = background.colors[upper].percentage;
            t = end > start ? clamp((t - start) / (end - start), 0.0, 1.0)
                            : (t < end ? 0.0 : 1.0);

            GradientColor segment = prepare_gradient_pair(
                background.color_space,
                background.colors[lower].color,
                background.colors[upper].color
            );

            switch (background.color_space) {
                case 0:
                    color = lerp(segment.color0, segment.color1, t);
                    break;
                case 1: {
                    float4 oklab_color = lerp(segment.color0, segment.color1, t);
                    color = oklab_to_srgb(oklab_color);
                    break;
                }
            }

            // Dither to reduce banding in gradients (especially dark/alpha).
            // Triangular-distributed noise breaks up 8-bit quantization steps.
            // ±2/255 for RGB (enough for dark-on-dark compositing),
            // ±3/255 for alpha (needs more because alpha × dark color = tiny steps).
            {
                // Integer screen-pixel hashing is stable across Direct3D
                // adapters; transcendental float hashes are not required to agree.
                float tri = gradient_dither((uint2)position);
                color.rgb += tri * 2.0 / 255.0;
                color.a   += tri * 3.0 / 255.0;
            }

            break;
        }
        case 2: {
            float gradient_angle_or_pattern_height = background.gradient_angle_or_pattern_height;
            float pattern_width = (gradient_angle_or_pattern_height / 65535.0f) / 255.0f;
            float pattern_interval = fmod(gradient_angle_or_pattern_height, 65535.0f) / 255.0f;
            float pattern_height = pattern_width + pattern_interval;
            float stripe_angle = M_PI_F / 4.0;
            float pattern_period = pattern_height * sin(stripe_angle);
            float2x2 rotation = rotate2d(stripe_angle);
            float2 relative_position = position - bounds.origin;
            float2 rotated_point = mul(relative_position, rotation);
            float pattern = fmod(rotated_point.x, pattern_period);
            float distance = min(pattern, pattern_period - pattern) - pattern_period * (pattern_width / pattern_height) /  2.0f;
            color = solid_color;
            color.a *= saturate(0.5 - distance);
            break;
        }
        case 3: {
            // checkerboard
            float size = background.gradient_angle_or_pattern_height;
            float2 relative_position = position - bounds.origin;

            float x_index = floor(relative_position.x / size);
            float y_index = floor(relative_position.y / size);
            float should_be_colored = (x_index + y_index) % 2.0;

            color = solid_color;
            color.a *= saturate(should_be_colored);
            break;
        }
    }

    return color;
}

// Returns the dash velocity of a corner given the dash velocity of the two
// sides, by returning the slower velocity (larger dashes).
//
// Since 0 is used for dash velocity when the border width is 0 (instead of
// +inf), this returns the other dash velocity in that case.
//
// An alternative to this might be to appropriately interpolate the dash
// velocity around the corner, but that seems overcomplicated.
float corner_dash_velocity(float dv1, float dv2) {
    if (dv1 == 0.0) {
        return dv2;
    } else if (dv2 == 0.0) {
        return dv1;
    } else {
        return min(dv1, dv2);
    }
}

// Returns alpha used to render antialiased dashes.
// `t` is within the dash when `fmod(t, period) < length`.
float dash_alpha(
    float t, float period, float length, float dash_velocity,
    float antialias_threshold
) {
    float half_period = period / 2.0;
    float half_length = length / 2.0;
    // Value in [-half_period, half_period]
    // The dash is in [-half_length, half_length]
    float centered = fmod(t + half_period - half_length, period) - half_period;
    // Signed distance for the dash, negative values are inside the dash
    float signed_distance = abs(centered) - half_length;
    // Antialiased alpha based on the signed distance
    return saturate(antialias_threshold - signed_distance / dash_velocity);
}

// This approximates distance to the nearest point to a quarter ellipse in a way
// that is sufficient for anti-aliasing when the ellipse is not very eccentric.
// The components of `point` are expected to be positive.
//
// Negative on the outside and positive on the inside.
float quarter_ellipse_sdf(float2 pt, float2 radii) {
    // Scale the space to treat the ellipse like a unit circle
    float2 circle_vec = pt / radii;
    float unit_circle_sdf = length(circle_vec) - 1.0;
    // Approximate up-scaling of the length by using the average of the radii.
    //
    // TODO: A better solution would be to use the gradient of the implicit
    // function for an ellipse to approximate a scaling factor.
    return unit_circle_sdf * (radii.x + radii.y) * -0.5;
}

/*
**
**              Quads
**
*/

struct Quad {
    uint order;
    uint border_style;
    Bounds bounds;
    Bounds content_mask;
    Background background;
    Hsla border_color;
    Corners corner_radii;
    Edges border_widths;
    uint2 clip_id;
};

struct QuadVertexOutput {
    nointerpolation uint quad_id: TEXCOORD0;
    float4 position: SV_Position;
    nointerpolation float4 border_color: COLOR0;
    nointerpolation float4 background_solid: COLOR1;
    nointerpolation float4 background_color0: COLOR2;
    nointerpolation float4 background_color1: COLOR3;
    float4 clip_distance: SV_ClipDistance;
};

struct QuadFragmentInput {
    nointerpolation uint quad_id: TEXCOORD0;
    float4 position: SV_Position;
    nointerpolation float4 border_color: COLOR0;
    nointerpolation float4 background_solid: COLOR1;
    nointerpolation float4 background_color0: COLOR2;
    nointerpolation float4 background_color1: COLOR3;
};

StructuredBuffer<Quad> quads: register(t1);

QuadVertexOutput quad_vertex(uint vertex_id: SV_VertexID, uint instance_id: SV_InstanceID) {
    float2 unit_vertex = float2(float(vertex_id & 1u), 0.5 * float(vertex_id & 2u));
    uint quad_id = batch_start_index + instance_id;
    Quad quad = quads[quad_id];
    float4 device_position = to_device_position(unit_vertex, quad.bounds);

    GradientColor gradient = prepare_gradient_color(
        quad.background.tag,
        quad.background.color_space,
        quad.background.solid,
        quad.background.colors
    );
    float4 clip_distance = distance_from_clip_rect(unit_vertex, quad.bounds, quad.content_mask);
    float4 border_color = hsla_to_rgba(quad.border_color);

    QuadVertexOutput output;
    output.position = device_position;
    output.border_color = border_color;
    output.quad_id = quad_id;
    output.background_solid = gradient.solid;
    output.background_color0 = gradient.color0;
    output.background_color1 = gradient.color1;
    output.clip_distance = clip_distance;
    return output;
}

float4 quad_color(QuadFragmentInput input) {
    Quad quad = quads[input.quad_id];
    float4 background_color = gradient_color(quad.background, input.position.xy, quad.bounds,
    input.background_solid, input.background_color0, input.background_color1);

    bool unrounded = quad.corner_radii.top_left == 0.0 &&
        quad.corner_radii.top_right == 0.0 &&
        quad.corner_radii.bottom_left == 0.0 &&
        quad.corner_radii.bottom_right == 0.0;

    // Fast path when the quad is not rounded and doesn't have any border
    if (quad.border_widths.top == 0.0 &&
        quad.border_widths.left == 0.0 &&
        quad.border_widths.right == 0.0 &&
        quad.border_widths.bottom == 0.0 &&
        unrounded) {
        return background_color;
    }

    float2 size = quad.bounds.size;
    float2 half_size = size / 2.;
    float2 the_point = input.position.xy - quad.bounds.origin;
    float2 center_to_point = the_point - half_size;

    // Signed distance field threshold for inclusion of pixels. 0.5 is the
    // minimum distance between the center of the pixel and the edge.
    const float antialias_threshold = 0.5;

    // Radius of the nearest corner
    float corner_radius = pick_corner_radius(center_to_point, quad.corner_radii);

    float2 border = float2(
        center_to_point.x < 0.0 ? quad.border_widths.left : quad.border_widths.right,
        center_to_point.y < 0.0 ? quad.border_widths.top : quad.border_widths.bottom
    );

    // 0-width borders are reduced so that `inner_sdf >= antialias_threshold`.
    // The purpose of this is to not draw antialiasing pixels in this case.
    float2 reduced_border = float2(
        border.x == 0.0 ? -antialias_threshold : border.x,
        border.y == 0.0 ? -antialias_threshold : border.y
    );

    // Vector from the corner of the quad bounds to the point, after mirroring
    // the point into the bottom right quadrant. Both components are <= 0.
    float2 corner_to_point = abs(center_to_point) - half_size;

    // Vector from the point to the center of the rounded corner's circle, also
    // mirrored into bottom right quadrant.
    float2 corner_center_to_point = corner_to_point + corner_radius;

    // Whether the nearest point on the border is rounded
    bool is_near_rounded_corner =
        corner_center_to_point.x >= 0.0 &&
        corner_center_to_point.y >= 0.0;

    // Vector from straight border inner corner to point.
    //
    // 0-width borders are turned into width -1 so that inner_sdf is > 1.0 near
    // the border. Without this, antialiasing pixels would be drawn.
    float2 straight_border_inner_corner_to_point = corner_to_point + reduced_border;

    // Whether the point is beyond the inner edge of the straight border
    bool is_beyond_inner_straight_border =
        straight_border_inner_corner_to_point.x > 0.0 ||
        straight_border_inner_corner_to_point.y > 0.0;

    // Whether the point is far enough inside the quad, such that the pixels are
    // not affected by the straight border.
    bool is_within_inner_straight_border =
        straight_border_inner_corner_to_point.x < -antialias_threshold &&
        straight_border_inner_corner_to_point.y < -antialias_threshold;

    // Fast path for points that must be part of the background
    if (is_within_inner_straight_border && !is_near_rounded_corner) {
        return background_color;
    }

    // Signed distance of the point to the outside edge of the quad's border
    float outer_sdf = quad_sdf_impl(corner_center_to_point, corner_radius);

    // Approximate signed distance of the point to the inside edge of the quad's
    // border. It is negative outside this edge (within the border), and
    // positive inside.
    //
    // This is not always an accurate signed distance:
    // * The rounded portions with varying border width use an approximation of
    //   nearest-point-on-ellipse.
    // * When it is quickly known to be outside the edge, -1.0 is used.
    float inner_sdf = 0.0;
    if (corner_center_to_point.x <= 0.0 || corner_center_to_point.y <= 0.0) {
        // Fast paths for straight borders
        inner_sdf = -max(straight_border_inner_corner_to_point.x,
                        straight_border_inner_corner_to_point.y);
    } else if (is_beyond_inner_straight_border) {
        // Fast path for points that must be outside the inner edge
        inner_sdf = -1.0;
    } else if (reduced_border.x == reduced_border.y) {
        // Fast path for circular inner edge.
        inner_sdf = -(outer_sdf + reduced_border.x);
    } else {
        float2 ellipse_radii = max(float2(0.0, 0.0), float2(corner_radius, corner_radius) - reduced_border);
        inner_sdf = quarter_ellipse_sdf(corner_center_to_point, ellipse_radii);
    }

    // Negative when inside the border
    float border_sdf = max(inner_sdf, outer_sdf);

    float4 color = background_color;
    if (border_sdf < antialias_threshold) {
        float4 border_color = input.border_color;
        // Dashed border logic when border_style == 1
        if (quad.border_style == 1) {
            // Position along the perimeter in "dash space", where each dash
            // period has length 1
            float t = 0.0;

            // Total number of dash periods, so that the dash spacing can be
            // adjusted to evenly divide it
            float max_t = 0.0;

            // Border width is proportional to dash size. This is the behavior
            // used by browsers, but also avoids dashes from different segments
            // overlapping when dash size is smaller than the border width.
            //
            // Dash pattern: (2 * border width) dash, (1 * border width) gap
            const float dash_length_per_width = 2.0;
            const float dash_gap_per_width = 1.0;
            const float dash_period_per_width = dash_length_per_width + dash_gap_per_width;

            // Since the dash size is determined by border width, the density of
            // dashes varies. Multiplying a pixel distance by this returns a
            // position in dash space - it has units (dash period / pixels). So
            // a dash velocity of (1 / 10) is 1 dash every 10 pixels.
            float dash_velocity = 0.0;

            // Dividing this by the border width gives the dash velocity
            const float dv_numerator = 1.0 / dash_period_per_width;

            if (unrounded) {
                // When corners aren't rounded, the dashes are separately laid
                // out on each straight line, rather than around the whole
                // perimeter. This way each line starts and ends with a dash.
                bool is_horizontal = corner_center_to_point.x < corner_center_to_point.y;
                // Choosing the right border width for dashed borders.
                // TODO: A better solution exists taking a look at the whole file.
                // this does not fix single dashed borders at the corners
                float2 dashed_border = float2(
                    max(quad.border_widths.bottom, quad.border_widths.top),
                    max(quad.border_widths.right, quad.border_widths.left)
                );
                float border_width = is_horizontal ? dashed_border.x : dashed_border.y;
                dash_velocity = dv_numerator / border_width;
                t = is_horizontal ? the_point.x : the_point.y;
                t *= dash_velocity;
                max_t = is_horizontal ? size.x : size.y;
                max_t *= dash_velocity;
            } else {
                // When corners are rounded, the dashes are laid out clockwise
                // around the whole perimeter.

                float r_tr = quad.corner_radii.top_right;
                float r_br = quad.corner_radii.bottom_right;
                float r_bl = quad.corner_radii.bottom_left;
                float r_tl = quad.corner_radii.top_left;

                float w_t = quad.border_widths.top;
                float w_r = quad.border_widths.right;
                float w_b = quad.border_widths.bottom;
                float w_l = quad.border_widths.left;

                // Straight side dash velocities
                float dv_t = w_t <= 0.0 ? 0.0 : dv_numerator / w_t;
                float dv_r = w_r <= 0.0 ? 0.0 : dv_numerator / w_r;
                float dv_b = w_b <= 0.0 ? 0.0 : dv_numerator / w_b;
                float dv_l = w_l <= 0.0 ? 0.0 : dv_numerator / w_l;

                // Straight side lengths in dash space
                float s_t = (size.x - r_tl - r_tr) * dv_t;
                float s_r = (size.y - r_tr - r_br) * dv_r;
                float s_b = (size.x - r_br - r_bl) * dv_b;
                float s_l = (size.y - r_bl - r_tl) * dv_l;

                float corner_dash_velocity_tr = corner_dash_velocity(dv_t, dv_r);
                float corner_dash_velocity_br = corner_dash_velocity(dv_b, dv_r);
                float corner_dash_velocity_bl = corner_dash_velocity(dv_b, dv_l);
                float corner_dash_velocity_tl = corner_dash_velocity(dv_t, dv_l);

                // Corner lengths in dash space
                float c_tr = r_tr * (M_PI_F / 2.0) * corner_dash_velocity_tr;
                float c_br = r_br * (M_PI_F / 2.0) * corner_dash_velocity_br;
                float c_bl = r_bl * (M_PI_F / 2.0) * corner_dash_velocity_bl;
                float c_tl = r_tl * (M_PI_F / 2.0) * corner_dash_velocity_tl;

                // Cumulative dash space upto each segment
                float upto_tr = s_t;
                float upto_r = upto_tr + c_tr;
                float upto_br = upto_r + s_r;
                float upto_b = upto_br + c_br;
                float upto_bl = upto_b + s_b;
                float upto_l = upto_bl + c_bl;
                float upto_tl = upto_l + s_l;
                max_t = upto_tl + c_tl;

                if (is_near_rounded_corner) {
                    float radians = atan2(corner_center_to_point.y, corner_center_to_point.x);
                    float corner_t = radians * corner_radius;

                    if (center_to_point.x >= 0.0) {
                        if (center_to_point.y < 0.0) {
                            dash_velocity = corner_dash_velocity_tr;
                            // Subtracted because radians is pi/2 to 0 when
                            // going clockwise around the top right corner,
                            // since the y axis has been flipped
                            t = upto_r - corner_t * dash_velocity;
                        } else {
                            dash_velocity = corner_dash_velocity_br;
                            // Added because radians is 0 to pi/2 when going
                            // clockwise around the bottom-right corner
                            t = upto_br + corner_t * dash_velocity;
                        }
                    } else {
                        if (center_to_point.y >= 0.0) {
                            dash_velocity = corner_dash_velocity_bl;
                            // Subtracted because radians is pi/1 to 0 when
                            // going clockwise around the bottom-left corner,
                            // since the x axis has been flipped
                            t = upto_l - corner_t * dash_velocity;
                        } else {
                            dash_velocity = corner_dash_velocity_tl;
                            // Added because radians is 0 to pi/2 when going
                            // clockwise around the top-left corner, since both
                            // axis were flipped
                            t = upto_tl + corner_t * dash_velocity;
                        }
                    }
                } else {
                    // Straight borders
                    bool is_horizontal = corner_center_to_point.x < corner_center_to_point.y;
                    if (is_horizontal) {
                        if (center_to_point.y < 0.0) {
                            dash_velocity = dv_t;
                            t = (the_point.x - r_tl) * dash_velocity;
                        } else {
                            dash_velocity = dv_b;
                            t = upto_bl - (the_point.x - r_bl) * dash_velocity;
                        }
                    } else {
                        if (center_to_point.x < 0.0) {
                            dash_velocity = dv_l;
                            t = upto_tl - (the_point.y - r_tl) * dash_velocity;
                        } else {
                            dash_velocity = dv_r;
                            t = upto_r + (the_point.y - r_tr) * dash_velocity;
                        }
                    }
                }
            }
            float dash_length = dash_length_per_width / dash_period_per_width;
            float desired_dash_gap = dash_gap_per_width / dash_period_per_width;

            // Straight borders should start and end with a dash, so max_t is
            // reduced to cause this.
            max_t -= unrounded ? dash_length : 0.0;
            if (max_t >= 1.0) {
                // Adjust dash gap to evenly divide max_t
                float dash_count = floor(max_t);
                float dash_period = max_t / dash_count;
                border_color.a *= dash_alpha(t, dash_period, dash_length, dash_velocity, antialias_threshold);
            } else if (unrounded) {
                // When there isn't enough space for the full gap between the
                // two start / end dashes of a straight border, reduce gap to
                // make them fit.
                float dash_gap = max_t - dash_length;
                if (dash_gap > 0.0) {
                    float dash_period = dash_length + dash_gap;
                    border_color.a *= dash_alpha(t, dash_period, dash_length, dash_velocity, antialias_threshold);
                }
            }
        }

        // Blend the border on top of the background and then linearly interpolate
        // between the two as we slide inside the background.
        float4 blended_border = over(background_color, border_color);
        color = lerp(background_color, blended_border,
                    saturate(antialias_threshold - inner_sdf));
    }

    return color * float4(1.0, 1.0, 1.0, saturate(antialias_threshold - outer_sdf));
}

/*
**
**              Shadows
**
*/

struct Shadow {
    uint order;
    float blur_radius;
    Bounds bounds;
    Corners corner_radii;
    Bounds content_mask;
    Hsla color;
    Bounds element_bounds;
    Corners element_corner_radii;
    uint inset;
    // 1 = cut the element's own shape out of a drop shadow, so it rings the
    // element instead of painting under it.
    uint outer_only;
    uint2 clip_id;
};

struct ShadowVertexOutput {
    nointerpolation uint shadow_id: TEXCOORD0;
    float4 position: SV_Position;
    nointerpolation float4 color: COLOR;
    float4 clip_distance: SV_ClipDistance;
};

struct ShadowFragmentInput {
  nointerpolation uint shadow_id: TEXCOORD0;
  float4 position: SV_Position;
  nointerpolation float4 color: COLOR;
};

StructuredBuffer<Shadow> shadows: register(t1);

ShadowVertexOutput shadow_vertex(uint vertex_id: SV_VertexID, uint instance_id: SV_InstanceID) {
    float2 unit_vertex = float2(float(vertex_id & 1u), 0.5 * float(vertex_id & 2u));
    uint shadow_id = batch_start_index + instance_id;
    Shadow shadow = shadows[shadow_id];

    Bounds bounds;
    if (shadow.inset != 0u) {
        bounds = shadow.element_bounds;
    } else {
        // Leave room for the gaussian tail outside the shadow rect.
        float margin = 3.0 * shadow.blur_radius;
        bounds = shadow.bounds;
        bounds.origin -= margin;
        bounds.size += 2.0 * margin;
    }

    float4 device_position = to_device_position(unit_vertex, bounds);
    float4 clip_distance = distance_from_clip_rect(unit_vertex, bounds, shadow.content_mask);
    float4 color = hsla_to_rgba(shadow.color);

    ShadowVertexOutput output;
    output.position = device_position;
    output.color = color;
    output.shadow_id = shadow_id;
    output.clip_distance = clip_distance;

    return output;
}

float4 shadow_color(ShadowFragmentInput input) {
    Shadow shadow = shadows[input.shadow_id];

    float2 half_size = shadow.bounds.size / 2.;
    float2 center = shadow.bounds.origin + half_size;
    float2 point0 = input.position.xy - center;
    float corner_radius = pick_corner_radius(point0, shadow.corner_radii);

    float alpha;
    if (shadow.blur_radius == 0.) {
        float distance = quad_sdf(input.position.xy, shadow.bounds, shadow.corner_radii);
        alpha = saturate(0.5 - distance);
    } else {
        // The signal is only non-zero in a limited range, so don't waste samples
        float low = point0.y - half_size.y;
        float high = point0.y + half_size.y;
        float start = clamp(-3. * shadow.blur_radius, low, high);
        float end = clamp(3. * shadow.blur_radius, low, high);

        // Accumulate samples (we can get away with surprisingly few samples)
        float step = (end - start) / 4.;
        float y = start + step * 0.5;
        alpha = 0.;
        for (int i = 0; i < 4; i++) {
            alpha += blur_along_x(point0.x, point0.y - y, shadow.blur_radius,
                                corner_radius, half_size) *
                    gaussian(y, shadow.blur_radius) * step;
            y += step;
        }
    }

    if (shadow.inset != 0u) {
        // The inset shadow is the complement of the (blurred) hole rect, clipped to the element.
        // `saturate(0.5 - d)` gives a 1-pixel antialiased edge: d <= -0.5 -> 1, d >= 0.5 -> 0.
        alpha = 1.0 - alpha;
        float element_distance = quad_sdf(input.position.xy, shadow.element_bounds,
                                          shadow.element_corner_radii);
        alpha *= saturate(0.5 - element_distance);
    } else if (shadow.outer_only != 0u) {
        // A ring keeps only what falls outside the element, the way CSS clips an
        // outer box-shadow to the border box.
        float element_distance = quad_sdf(input.position.xy, shadow.element_bounds,
                                          shadow.element_corner_radii);
        alpha *= saturate(0.5 + element_distance);
    }

    return input.color * float4(1., 1., 1., alpha);
}

/*
**
**              Path Rasterization
**
*/

struct PathRasterizationSprite {
    float2 xy_position;
    float2 st_position;
    Background color;
    Bounds bounds;
    uint2 clip_id;
};

StructuredBuffer<PathRasterizationSprite> path_rasterization_sprites: register(t1);

struct PathVertexOutput {
    float4 position: SV_Position;
    float2 st_position: TEXCOORD0;
    nointerpolation uint vertex_id: TEXCOORD1;
    float4 clip_distance: SV_ClipDistance;
};

struct PathFragmentInput {
    float4 position: SV_Position;
    float2 st_position: TEXCOORD0;
    nointerpolation uint vertex_id: TEXCOORD1;
};

PathVertexOutput path_rasterization_vertex(uint vertex_id: SV_VertexID) {
    PathRasterizationSprite sprite = path_rasterization_sprites[vertex_id];

    PathVertexOutput output;
    output.position = to_device_position_impl(sprite.xy_position);
    output.st_position = sprite.st_position;
    output.vertex_id = vertex_id;
    output.clip_distance = distance_from_clip_rect_impl(sprite.xy_position, sprite.bounds);

    return output;
}

float4 path_rasterization_color(PathFragmentInput input) {
    float2 dx = ddx(input.st_position);
    float2 dy = ddy(input.st_position);
    PathRasterizationSprite sprite = path_rasterization_sprites[input.vertex_id];

    Background background = sprite.color;
    Bounds bounds = sprite.bounds;

    float alpha;
    if (length(float2(dx.x, dy.x))) {
        alpha = 1.0;
    } else {
        float2 gradient = 2.0 * input.st_position.xx * float2(dx.x, dy.x) - float2(dx.y, dy.y);
        float f = input.st_position.x * input.st_position.x - input.st_position.y;
        float distance = f / length(gradient);
        alpha = saturate(0.5 - distance);
    }

    GradientColor gradient = prepare_gradient_color(
        background.tag, background.color_space, background.solid, background.colors);

    float4 color = gradient_color(background, input.position.xy, bounds,
        gradient.solid, gradient.color0, gradient.color1);
    return float4(color.rgb * color.a * alpha, alpha * color.a);
}

/*
**
**              Path Sprites
**
*/

struct PathSprite {
    Bounds bounds;
};

struct PathSpriteVertexOutput {
    float4 position: SV_Position;
    float2 texture_coords: TEXCOORD0;
};

StructuredBuffer<PathSprite> path_sprites: register(t1);

PathSpriteVertexOutput path_sprite_vertex(uint vertex_id: SV_VertexID, uint sprite_id: SV_InstanceID) {
    float2 unit_vertex = float2(float(vertex_id & 1u), 0.5 * float(vertex_id & 2u));
    PathSprite sprite = path_sprites[sprite_id];

    // Don't apply content mask because it was already accounted for when rasterizing the path
    float4 device_position = to_device_position(unit_vertex, sprite.bounds);

    float2 screen_position = sprite.bounds.origin + unit_vertex * sprite.bounds.size;
    float2 texture_coords = screen_position / global_viewport_size;

    PathSpriteVertexOutput output;
    output.position = device_position;
    output.texture_coords = texture_coords;
    return output;
}

float4 path_sprite_fragment(PathSpriteVertexOutput input): SV_Target {
    return t_sprite.Sample(s_sprite, input.texture_coords);
}

/*
**
**              Underlines
**
*/

struct Underline {
    uint order;
    uint pad;
    Bounds bounds;
    Bounds content_mask;
    Hsla color;
    float thickness;
    uint wavy;
    uint2 clip_id;
};

struct UnderlineVertexOutput {
  nointerpolation uint underline_id: TEXCOORD0;
  float4 position: SV_Position;
  nointerpolation float4 color: COLOR;
  float4 clip_distance: SV_ClipDistance;
};

struct UnderlineFragmentInput {
  nointerpolation uint underline_id: TEXCOORD0;
  float4 position: SV_Position;
  nointerpolation float4 color: COLOR;
};

StructuredBuffer<Underline> underlines: register(t1);

UnderlineVertexOutput underline_vertex(uint vertex_id: SV_VertexID, uint instance_id: SV_InstanceID) {
    float2 unit_vertex = float2(float(vertex_id & 1u), 0.5 * float(vertex_id & 2u));
    uint underline_id = batch_start_index + instance_id;
    Underline underline = underlines[underline_id];
    float4 device_position = to_device_position(unit_vertex, underline.bounds);
    float4 clip_distance = distance_from_clip_rect(unit_vertex, underline.bounds,
                                                    underline.content_mask);
    float4 color = hsla_to_rgba(underline.color);

    UnderlineVertexOutput output;
    output.position = device_position;
    output.color = color;
    output.underline_id = underline_id;
    output.clip_distance = clip_distance;
    return output;
}

float4 underline_color(UnderlineFragmentInput input) {
    const float WAVE_FREQUENCY = 2.0;
    const float WAVE_HEIGHT_RATIO = 0.8;

    Underline underline = underlines[input.underline_id];
    if (underline.wavy) {
        float half_thickness = underline.thickness * 0.5;
        float2 origin = underline.bounds.origin;

        float2 st = ((input.position.xy - origin) / underline.bounds.size.y) - float2(0., 0.5);
        float frequency = (M_PI_F * WAVE_FREQUENCY * underline.thickness) / underline.bounds.size.y;
        float amplitude = (underline.thickness * WAVE_HEIGHT_RATIO) / underline.bounds.size.y;

        float sine = sin(st.x * frequency) * amplitude;
        float dSine = cos(st.x * frequency) * amplitude * frequency;
        float distance = (st.y - sine) / sqrt(1. + dSine * dSine);
        float distance_in_pixels = distance * underline.bounds.size.y;
        float distance_from_top_border = distance_in_pixels - half_thickness;
        float distance_from_bottom_border = distance_in_pixels + half_thickness;
        float alpha = saturate(
            0.5 - max(-distance_from_bottom_border, distance_from_top_border));
        return input.color * float4(1., 1., 1., alpha);
    } else {
        return input.color;
    }
}

/*
**
**              Monochrome sprites
**
*/

struct MonochromeSprite {
    uint order;
    uint pad;
    Bounds bounds;
    Bounds content_mask;
    Hsla color;
    AtlasTile tile;
    TransformationMatrix transformation;
    uint2 clip_id;
};

struct MonochromeSpriteVertexOutput {
    float4 position: SV_Position;
    float2 tile_position: POSITION;
    nointerpolation float4 color: COLOR;
    float4 clip_distance: SV_ClipDistance;
    nointerpolation uint clip_id: TEXCOORD2;
};

struct MonochromeSpriteFragmentInput {
    float4 position: SV_Position;
    float2 tile_position: POSITION;
    nointerpolation float4 color: COLOR;
    float4 clip_distance: SV_ClipDistance;
    nointerpolation uint clip_id: TEXCOORD2;
};

StructuredBuffer<MonochromeSprite> mono_sprites: register(t1);

MonochromeSpriteVertexOutput monochrome_sprite_vertex(uint vertex_id: SV_VertexID, uint instance_id: SV_InstanceID) {
    float2 unit_vertex = float2(float(vertex_id & 1u), 0.5 * float(vertex_id & 2u));
    uint sprite_id = batch_start_index + instance_id;
    MonochromeSprite sprite = mono_sprites[sprite_id];
    float4 device_position =
        to_device_position_transformed(unit_vertex, sprite.bounds, sprite.transformation);
    float4 clip_distance = distance_from_clip_rect_transformed(unit_vertex, sprite.bounds, sprite.content_mask, sprite.transformation);
    float2 tile_position = to_tile_position(unit_vertex, sprite.tile);
    float4 color = hsla_to_rgba(sprite.color);

    MonochromeSpriteVertexOutput output;
    output.position = device_position;
    output.tile_position = tile_position;
    output.color = color;
    output.clip_distance = clip_distance;
    output.clip_id = sprite.clip_id.x;
    return output;
}

float4 monochrome_sprite_color(MonochromeSpriteFragmentInput input) {
    float sample = t_sprite.Sample(s_sprite, input.tile_position).r;
    float alpha_corrected = apply_contrast_and_gamma_correction(sample, input.color.rgb, grayscale_enhanced_contrast, gamma_ratios);
    return float4(input.color.rgb, input.color.a * alpha_corrected);
}

MonochromeSpriteVertexOutput subpixel_sprite_vertex(uint vertex_id: SV_VertexID, uint instance_id: SV_InstanceID) {
    return monochrome_sprite_vertex(vertex_id, instance_id);
}

SubpixelSpriteFragmentOutput subpixel_sprite_fragment(MonochromeSpriteFragmentInput input) {
    float3 sample = t_sprite.Sample(s_sprite, input.tile_position).rgb;
    if (is_bgr) {
        sample = sample.bgr;
    }
    float3 alpha_corrected = apply_contrast_and_gamma_correction3(sample, input.color.rgb, subpixel_enhanced_contrast, gamma_ratios);

    SubpixelSpriteFragmentOutput output;
    output.foreground = float4(input.color.rgb, 1.0f);
    output.alpha = float4(input.color.a * alpha_corrected, 1.0f);
    if (input.clip_id != 0u) output.alpha *= rounded_clip_coverage(input.position.xy, input.clip_id);
    return output;
}

MonochromeSpriteVertexOutput overlay_subpixel_sprite_vertex(uint vertex_id: SV_VertexID, uint instance_id: SV_InstanceID) {
    return monochrome_sprite_vertex(vertex_id, instance_id);
}

float4 overlay_subpixel_sprite_fragment(MonochromeSpriteFragmentInput input): SV_Target {
    float3 sample = t_sprite.Sample(s_sprite, input.tile_position).rgb;
    if (is_bgr) {
        sample = sample.bgr;
    }
    float3 alpha_corrected = apply_contrast_and_gamma_correction3(sample, input.color.rgb, subpixel_enhanced_contrast, gamma_ratios);
    float coverage = max(alpha_corrected.r, max(alpha_corrected.g, alpha_corrected.b));
    return clipped_color(float4(input.color.rgb, input.color.a * coverage), input.position.xy, input.clip_id, false);
}

/*
**
**              Polychrome sprites
**
*/

struct PolychromeSprite {
    uint order;
    uint blend_mode;
    uint color_mode;
    uint sample_inset;
    Bounds bounds;
    Bounds content_mask;
    Corners corner_radii;
    AtlasTile tile;
    TransformationMatrix transformation;
    Hsla tint;
    float opacity;
    uint pad;
    uint2 clip_id;
};

struct PolychromeSpriteVertexOutput {
    nointerpolation uint sprite_id: TEXCOORD0;
    float4 position: SV_Position;
    float2 tile_position: POSITION;
    float2 local_position: TEXCOORD1;
    float4 clip_distance: SV_ClipDistance;
};

struct PolychromeSpriteFragmentInput {
    nointerpolation uint sprite_id: TEXCOORD0;
    float4 position: SV_Position;
    float2 tile_position: POSITION;
    float2 local_position: TEXCOORD1;
};

StructuredBuffer<PolychromeSprite> poly_sprites: register(t1);

PolychromeSpriteVertexOutput polychrome_sprite_vertex(uint vertex_id: SV_VertexID, uint instance_id: SV_InstanceID) {
    float2 unit_vertex = float2(float(vertex_id & 1u), 0.5 * float(vertex_id & 2u));
    uint sprite_id = batch_start_index + instance_id;
    PolychromeSprite sprite = poly_sprites[sprite_id];
    float4 device_position = to_device_position_transformed(
        unit_vertex, sprite.bounds, sprite.transformation);
    float4 clip_distance = distance_from_clip_rect_transformed(
        unit_vertex, sprite.bounds, sprite.content_mask, sprite.transformation);
    float2 tile_position = sprite.sample_inset != 0u
        ? to_inset_tile_position(unit_vertex, sprite.tile)
        : to_tile_position(unit_vertex, sprite.tile);

    PolychromeSpriteVertexOutput output;
    output.position = device_position;
    output.tile_position = tile_position;
    output.sprite_id = sprite_id;
    output.local_position = sprite.bounds.origin + unit_vertex * sprite.bounds.size;
    output.clip_distance = clip_distance;
    return output;
}

float4 polychrome_sprite_color(PolychromeSpriteFragmentInput input) {
    PolychromeSprite sprite = poly_sprites[input.sprite_id];
    float4 sample = t_sprite.Sample(s_sprite, input.tile_position);
    float distance = quad_sdf(input.local_position, sprite.bounds, sprite.corner_radii);
    float edge_width = quad_sdf_edge_width(input.local_position, sprite.bounds,
                                           sprite.corner_radii, sprite.transformation);
    float coverage = saturate(0.5 - distance / edge_width);

    float4 color = sample;
    if (sprite.color_mode == 1u) {
        float3 grayscale = dot(color.rgb, GRAYSCALE_FACTORS);
        color = float4(grayscale, sample.a);
    } else if (sprite.color_mode == 2u) {
        color = hsla_to_rgba(sprite.tint);
        color.a *= sample.a;
    }
    color.a *= sprite.opacity * coverage;
    if (sprite.blend_mode == 2u) {
        color.rgb *= color.a;
    }
    return color;
}

/*
**
**              Backdrop glass
**
*/

// The blur and replacement composite both draw one full-viewport triangle
// strip under an integral scissor and read `t_sprite`, which the renderer
// points at whichever scratch texture the pass is reading. `b2` carries what
// changes between them.
cbuffer BackdropGlassParams: register(b2) {
    // The separable gaussian's axis for this pass, and its sigma.
    float2 backdrop_direction;
    float backdrop_sigma;
    float backdrop_bevel;
    // The surface's own rounded rect, used when `backdrop_lobe_count` is 0.
    float4 backdrop_bounds;
    float4 backdrop_radii;
    float4 backdrop_mask;
    float backdrop_refraction;
    float backdrop_dispersion;
    float backdrop_specular;
    float backdrop_light_angle;
    float backdrop_specular_sharpness;
    float backdrop_smoothing;
    uint backdrop_lobe_count;
    uint backdrop_pad;
    float backdrop_transmission_gain;
    float backdrop_hairline;
    float backdrop_saturation;
    uint backdrop_clip_id;
    float4 backdrop_wash;
    float4 backdrop_optical_lift;
    // x = edge (0 none, 1 top, 2 bottom, 3 left, 4 right), y = band in pixels.
    float4 backdrop_edge_mask;
    float backdrop_thickness;
    float backdrop_refractive_index;
    float backdrop_depth;
    float backdrop_optics_pad;
    // Eight lobes, each a bounds and a radii. Written as an array of vectors
    // rather than of structs so that the sixteen-byte constant buffer packing
    // is the one the Rust side lays out.
    float4 backdrop_lobes[16];
};

struct BackdropVertexOutput {
    float4 position: SV_Position;
    float2 uv: TEXCOORD0;
};

BackdropVertexOutput backdrop_blur_vertex(uint vertex_id: SV_VertexID) {
    float2 unit_vertex = float2(float(vertex_id & 1u), 0.5 * float(vertex_id & 2u));
    BackdropVertexOutput output;
    output.position = float4(unit_vertex * float2(2.0, -2.0) + float2(-1.0, 1.0), 0.0, 1.0);
    output.uv = unit_vertex;
    return output;
}

// The composite draws the same strip. The entry pt is named separately
// because the shader table pairs a vertex and a fragment by module name.
BackdropVertexOutput backdrop_glass_vertex(uint vertex_id: SV_VertexID) {
    return backdrop_blur_vertex(vertex_id);
}

float backdrop_gaussian(float x, float sigma) {
    float sigma_squared = sigma * sigma;
    return exp(-(x * x) / (2.0 * sigma_squared)) / sqrt(2.0 * M_PI_F * sigma_squared);
}

// One axis of the separable gaussian. Mirrors `fs_blur` in
// backdrop_glass.wgsl, including its sixty-four tap budget, which is what
// `MAX_GLASS_SIGMA_PER_PASS` in scene.rs is derived from.
float4 backdrop_blur_fragment(BackdropVertexOutput input): SV_Target {
    float sigma = max(backdrop_sigma, 1.0);
    int radius = min(64, int(ceil(sigma * 3.0)));
    float center_weight = backdrop_gaussian(0.0, sigma);
    float4 color = t_sprite.Sample(s_sprite, input.uv) * center_weight;
    float weight = center_weight;
    for (int offset = 1; offset <= 64; offset++) {
        if (offset <= radius) {
            float sample_weight = backdrop_gaussian(float(offset), sigma);
            float2 delta = backdrop_direction * float(offset) / global_viewport_size;
            color += (t_sprite.Sample(s_sprite, input.uv + delta) +
                      t_sprite.Sample(s_sprite, input.uv - delta)) * sample_weight;
            weight += 2.0 * sample_weight;
        }
    }
    return color / weight;
}

// Analytic normal and distance. Mirrors `lobe_field` in backdrop_glass.wgsl
// and `glass_lobe_field` in scene.rs; ties select an incident face.
float3 backdrop_lobe_field(float2 pt, float4 bounds, float4 radii) {
    float2 half_size = bounds.zw * 0.5;
    float2 local = pt - (bounds.xy + half_size);
    float radius = local.x >= 0.0
        ? (local.y >= 0.0 ? radii.z : radii.y)
        : (local.y >= 0.0 ? radii.w : radii.x);
    float2 delta = abs(local) - half_size + radius;
    float2 outside = max(delta, float2(0.0, 0.0));
    float outside_length = length(outside);
    float2 gradient = delta.x > delta.y ? float2(1.0, 0.0) : float2(0.0, 1.0);
    if (radius != 0.0 && outside_length > 0.0) {
        gradient = outside / outside_length;
    }
    gradient *= float2(local.x >= 0.0 ? 1.0 : -1.0, local.y >= 0.0 ? 1.0 : -1.0);
    float distance = radius == 0.0 ? max(delta.x, delta.y)
        : outside_length + min(max(delta.x, delta.y), 0.0) - radius;
    return float3(gradient, distance);
}

// The polynomial smooth minimum. Mirrors `glass_smooth_min` in scene.rs.
float backdrop_smooth_min(float a, float b, float smoothing) {
    if (smoothing <= 0.0) {
        return min(a, b);
    }
    float h = max(smoothing - abs(a - b), 0.0) / smoothing;
    return min(a, b) - h * h * smoothing * 0.25;
}

// Analytic field of the shape. Smooth-min derivatives use h/2, with no
// intermediate normalization. Mirrors `glass_field` in scene.rs.
float3 backdrop_glass_field(float2 pt) {
    if (backdrop_lobe_count == 0u) {
        return backdrop_lobe_field(pt, backdrop_bounds, backdrop_radii);
    }
    float3 field = backdrop_lobe_field(pt, backdrop_lobes[0], backdrop_lobes[1]);
    for (uint index = 1u; index < 8u; index++) {
        if (index < backdrop_lobe_count) {
            float3 next = backdrop_lobe_field(pt, backdrop_lobes[index * 2u],
                                              backdrop_lobes[index * 2u + 1u]);
            float h = backdrop_smoothing > 0.0
                ? max(backdrop_smoothing - abs(field.z - next.z), 0.0) / backdrop_smoothing : 0.0;
            float weight = field.z <= next.z ? h * 0.5 : 1.0 - h * 0.5;
            field = float3(lerp(field.xy, next.xy, weight),
                           backdrop_smooth_min(field.z, next.z, backdrop_smoothing));
        }
    }
    return field;
}

// Linear fade from a named edge. Mirrors `glass_edge_mask` in scene.rs.
float backdrop_edge_mask_factor(float2 pt) {
    float edge = backdrop_edge_mask.x;
    float band = backdrop_edge_mask.y;
    if (edge <= 0.0 || band <= 0.0) {
        return 1.0;
    }
    if (edge < 1.5) {
        return 1.0 - saturate((pt.y - backdrop_bounds.y) / band);
    }
    if (edge < 2.5) {
        return 1.0 - saturate((backdrop_bounds.y + backdrop_bounds.w - pt.y) / band);
    }
    if (edge < 3.5) {
        return 1.0 - saturate((pt.x - backdrop_bounds.x) / band);
    }
    return 1.0 - saturate((backdrop_bounds.x + backdrop_bounds.z - pt.x) / band);
}

float4 apply_backdrop_edge_mask(float4 color, float2 pt) {
    float mask = backdrop_edge_mask_factor(pt);
    if (mask >= 1.0) {
        return color;
    }
    float4 original = t_backdrop_sharp.Load(int3(int2(pt), 0));
    return lerp(original, color, mask);
}

// One device-pixel SDF ramp. Replacement compositing restores partially
// covered pixels from the exact sharp draw-order snapshot.
float4 apply_backdrop_shape_coverage(float4 color, float2 pt, float3 field) {
    // Smooth unions are implicit fields rather than exact signed distances.
    // Normalize by the analytic derivative magnitude so the transition remains
    // one device pixel around fused bridges as well as individual lobes.
    float coverage = saturate(0.5 - field.z / max(length(field.xy), 1e-4));
    if (coverage >= 1.0) {
        return color;
    }
    float4 original = t_backdrop_sharp.Load(int3(int2(pt), 0));
    return lerp(original, color, coverage);
}

// The optical source and retained sharp snapshot painted back through the
// surface's shape and material.
// Mirrors `fs_composite` in backdrop_glass.wgsl and
// `backdrop_glass_fragment` in shaders.metal.
// Single-interface refraction to an effective optical plane, not volume tracing.
float2 backdrop_optical_displacement(float3 normal, float index, float distance) {
    if (index == 1.0) return float2(0.0, 0.0);
    float3 ray = refract(float3(0.0, 0.0, -1.0), normal, 1.0 / index);
    return ray.xy / max(-ray.z, 1e-4) * distance;
}

float4 backdrop_glass_color(BackdropVertexOutput input) {
    float2 pt = input.position.xy;
    float2 mask_end = backdrop_mask.xy + backdrop_mask.zw;
    float3 field = backdrop_glass_field(pt);
    float distance = field.z;
    if (pt.x < backdrop_mask.x || pt.y < backdrop_mask.y ||
        pt.x >= mask_end.x || pt.y >= mask_end.y || distance >= 0.5) {
        discard;
    }

    // A plain frost is an exact copy of its blurred source. Liquid reaches the
    // path below even with blur zero: scattering is not the optics switch.
    if ((backdrop_bevel <= 0.0 || backdrop_thickness == 0.0 || backdrop_refractive_index == 1.0) &&
        (backdrop_specular <= 0.0 || backdrop_refractive_index == 1.0) && backdrop_transmission_gain == 1.0 &&
        backdrop_saturation == 1.0 && backdrop_wash.a <= 0.0 &&
        backdrop_optical_lift.a <= 0.0 && backdrop_hairline <= 0.0) {
        return apply_backdrop_shape_coverage(
            apply_backdrop_edge_mask(t_sprite.Load(int3(int2(pt), 0)), pt), pt, field);
    }

    // Keep the smooth-union derivative magnitude for the height derivative.
    // Only the decorative hairline needs a unit contour direction.
    float2 gradient = field.xy;
    float gradient_length = length(gradient);
    if (gradient_length > 0.0) {
        gradient = gradient / gradient_length;
    }

    float u = backdrop_bevel > 0.0 ? saturate(1.0 + distance / backdrop_bevel) : 0.0;
    float profile = sqrt(max(1.0 - u * u, 0.0));
    float height = backdrop_thickness * (backdrop_refraction < 0.0 ? 1.0 - profile : profile);
    float3 normal = float3(0.0, 0.0, 1.0);
    if (backdrop_bevel > 0.0 && backdrop_thickness > 0.0) {
        normal = normalize(float3(field.xy * (backdrop_thickness / backdrop_bevel) *
            u / sqrt(max(1.0 - u * u, 1e-4)) * sign(backdrop_refraction), 1.0));
    }
    float travel = height + backdrop_depth;
    float index = backdrop_refractive_index;

    // Refract the scattered source even at the rim: mixing the sharp snapshot
    // back in would resurrect readable backdrop text. With blur zero this
    // source is already sharp, preserving Clear optics.
    float2 red_uv = (pt + backdrop_optical_displacement(normal, 1.0 + (index - 1.0) * (1.0 - backdrop_dispersion), travel)) / global_viewport_size;
    float2 green_uv = (pt + backdrop_optical_displacement(normal, index, travel)) / global_viewport_size;
    float2 blue_uv = (pt + backdrop_optical_displacement(normal, 1.0 + (index - 1.0) * (1.0 + backdrop_dispersion), travel)) / global_viewport_size;
    float4 frosted_red = t_sprite.Sample(s_sprite, red_uv);
    float4 frosted_green = t_sprite.Sample(s_sprite, green_uv);
    float4 frosted_blue = t_sprite.Sample(s_sprite, blue_uv);
    float4 color = float4(frosted_red.r, frosted_green.g, frosted_blue.b, frosted_green.a);

    // Sample (including the rim), saturation, gain, source-over wash, then lift.
    float luminance = dot(color.rgb, float3(0.2126, 0.7152, 0.0722));
    color.rgb = max(lerp(luminance.xxx, color.rgb, backdrop_saturation), 0.0);
    color.rgb *= backdrop_transmission_gain;
    color.rgb = lerp(color.rgb, backdrop_wash.rgb, backdrop_wash.a);
    color.a = backdrop_wash.a + color.a * (1.0 - backdrop_wash.a);
    color.rgb += backdrop_optical_lift.rgb * backdrop_optical_lift.a;

    if (backdrop_specular > 0.0 && index > 1.0) {
        // Analytic directional environment, not a real environment reflection.
        float3 light = normalize(float3(sin(backdrop_light_angle),
                                        -cos(backdrop_light_angle), 0.6));
        float3 reflected = reflect(float3(0.0, 0.0, -1.0), normal);
        float lobe_value = pow(max(dot(reflected, light), 0.0), backdrop_specular_sharpness);
        float f0 = pow((index - 1.0) / (index + 1.0), 2.0);
        float fresnel = f0 + (1.0 - f0) * pow(1.0 - normal.z, 5.0);
        color.rgb = lerp(color.rgb, lobe_value.xxx, saturate(backdrop_specular * fresnel));
    }

    if (backdrop_hairline > 0.0) {
        float hair = 1.0 - smoothstep(0.0, backdrop_hairline * 1.5, -distance);
        float facing_up = saturate(-gradient.y);
        color.rgb += hair * (1.0 - 0.18 * facing_up) * 0.18;
    }

    return apply_backdrop_shape_coverage(
        apply_backdrop_edge_mask(float4(saturate(color.rgb), color.a), pt), pt, field);
}

float4 quad_fragment(QuadFragmentInput input): SV_Target {
    return clipped_color(quad_color(input), input.position.xy, quads[input.quad_id].clip_id.x, false);
}
float4 shadow_fragment(ShadowFragmentInput input): SV_Target {
    return clipped_color(shadow_color(input), input.position.xy, shadows[input.shadow_id].clip_id.x, false);
}
float4 underline_fragment(UnderlineFragmentInput input): SV_Target {
    return clipped_color(underline_color(input), input.position.xy, underlines[input.underline_id].clip_id.x, false);
}
float4 monochrome_sprite_fragment(MonochromeSpriteFragmentInput input): SV_Target {
    return clipped_color(monochrome_sprite_color(input), input.position.xy, input.clip_id, false);
}
float4 polychrome_sprite_fragment(PolychromeSpriteFragmentInput input): SV_Target {
    PolychromeSprite sprite = poly_sprites[input.sprite_id];
    return clipped_color(polychrome_sprite_color(input), input.position.xy, sprite.clip_id.x, sprite.blend_mode == 2u);
}
float4 path_rasterization_fragment(PathFragmentInput input): SV_Target {
    return clipped_color(path_rasterization_color(input), input.position.xy, path_rasterization_sprites[input.vertex_id].clip_id.x, true);
}
float4 backdrop_glass_fragment(BackdropVertexOutput input): SV_Target {
    float4 optical = backdrop_glass_color(input);
    if (backdrop_clip_id == 0u) return optical;
    // Edge masking and shape coverage have already restored from this exact
    // snapshot. Apply inherited rounded clips last against the same pixels.
    float4 original = t_backdrop_sharp.Load(int3(int2(input.position.xy), 0));
    return lerp(original, optical, rounded_clip_coverage(input.position.xy, backdrop_clip_id));
}
